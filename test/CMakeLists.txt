function(add_ut_test TEST_NAME)
    add_executable(${TEST_NAME} ${TEST_NAME}.cpp)
    if ((CMAKE_CXX_COMPILER_ID MATCHES ".*Clang"))
        target_compile_options(${TEST_NAME} PRIVATE -Wall)
        target_link_options(${TEST_NAME} PRIVATE -Wall)
    else ()
        target_compile_options(${TEST_NAME} PRIVATE -fsanitize=address -Wall)
        target_link_options(${TEST_NAME} PRIVATE -fsanitize=address -Wall)
    endif ()
    target_include_directories(${TEST_NAME} PRIVATE ${CMAKE_BINARY_DIR}/include)
    target_link_libraries(${TEST_NAME} PRIVATE graph-prototype-options graph-prototype fmt refl-cpp ut)
    add_test(NAME ${TEST_NAME} COMMAND ${CMAKE_CROSSCOMPILING_EMULATOR} ${CMAKE_CURRENT_BINARY_DIR}/${TEST_NAME})
endfunction()

add_ut_test(qa_buffer)
add_ut_test(qa_dynamic_port)
add_ut_test(qa_dynamic_node)
add_ut_test(qa_filter)
add_ut_test(qa_settings)
add_ut_test(qa_tags)
add_ut_test(qa_scheduler)
add_ut_test(qa_thread_pool)
add_ut_test(qa_thread_affinity)

if (NOT EMSCRIPTEN)
    add_subdirectory(plugins)

    if (NOT (CMAKE_CXX_COMPILER_ID MATCHES ".*Clang"))
        # Clang has issues with ut, app_plugins_test (the target below) tests
        # the plugin system without ut
        add_ut_test(qa_plugins_test)
        target_link_libraries(qa_plugins_test PRIVATE graph-prototype-plugin)
        add_dependencies(qa_plugins_test good_math_plugin good_base_plugin bad_plugin)
    endif()

    add_executable(app_plugins_test app_plugins_test.cpp)
    target_compile_options(app_plugins_test PRIVATE -fsanitize=address -Wall)
    target_include_directories(app_plugins_test PRIVATE ${CMAKE_BINARY_DIR}/include)
    target_link_options(app_plugins_test PRIVATE -fsanitize=address -Wall)
    target_link_libraries(app_plugins_test PRIVATE graph-prototype-options graph-prototype graph-prototype-plugin fmt refl-cpp)
    add_dependencies(app_plugins_test good_math_plugin good_base_plugin bad_plugin)
    add_test(NAME app_plugins_test COMMAND ${CMAKE_CROSSCOMPILING_EMULATOR} ${CMAKE_CURRENT_BINARY_DIR}/app_plugins_test)
endif ()
